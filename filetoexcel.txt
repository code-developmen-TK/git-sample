Option Explicit

Sub ボタン1_Click()
'------------------------------------------------------------------
'Excelの全シートをHTMLのテーブルに変換してファイルに書き出す
'--------------------------------------------------------------------
    Dim Stream As Object
    Set Stream = CreateObject("ADODB.Stream")
    Stream.Charset = "utf-8"
    Stream.Type = 2 'text
    
    'Select folder to save file
    With Application.FileDialog(msoFileDialogFolderPicker)
        .Title = "Select a Folder to Save HTML Table"
        .AllowMultiSelect = False
        If .Show <> -1 Then Exit Sub
        Dim FolderPath As String
        FolderPath = .SelectedItems(1) & "\"
    End With
    
    Dim FileName As String
    FileName = "manual.html"

    Dim html As String
    html = html & "<!DOCTYPE html>" & vbNewLine
    html = html & "<html lang=""ja"">" & vbNewLine
    html = html & "<head>" & vbNewLine
    html = html & "<meta charset=""UTF-8"">" & vbNewLine
    html = html & "<meta name=""viewport"" content=""width=device-width, initial-scale=1.0"">" & vbNewLine
    html = html & "<link rel=""stylesheet"" href=""./css/reset.css"">" & vbNewLine
    html = html & "<link rel=""stylesheet"" type=""text/css"" href=""./css/manual.css"">" & vbNewLine
    html = html & "<title>マニュアル</title>" & vbNewLine
    html = html & "</head>" & vbNewLine
    html = html & "<body>" & vbNewLine
    
    Dim wb As Workbook
    Set wb = ActiveWorkbook
    
    Dim ws As Worksheet
    For Each ws In wb.Worksheets 'すべてのシートを書き出す
        
        'シートにデータが入力されているなら処理を実行する。
        If WorksheetFunction.CountA(ws.Cells) <> 0 Then
        
            '表の範囲を取得
            Dim rng As Range
            Set rng = ws.UsedRange
            
            '表の範囲を配列に変換
            Dim tbl As Variant
            tbl = rng.Value
            
            'テーブルのHTMLを作成
            html = html & "<table class=""manual"">" & vbNewLine
            html = html & "<caption>" & ws.Name & "</caption>" & vbNewLine
            html = html & " <thead>"
            html = html & "<tr>"

            Dim j As Long
            For j = 1 To rng.Columns.Count
                If j <> 3 Then
                    html = html & "<th>" & tbl(1, j) & "</th>"
                End If
            Next j
           
            
            html = html & "</tr>" & vbNewLine
            html = html & "</thead>" & vbNewLine
            html = html & "<tbody>" & vbNewLine

            Dim i As Long
            For i = 2 To rng.Rows.Count
                html = html & "<tr>"
                For j = 1 To rng.Columns.Count
                    If j = 3 Then
                        Const cst番号 As Long = 1 'No.
                        Const cst文書コード As Long = 2
                        Const cstフォルダ名 As Long = 3
                        Const cst文書名称 As Long = 4
                        Const cst承認日 As Long = 5
                        Const cst備考 As Long = 6
                                                
                        html = html & "<td><a class=""rink"" href=""./manual/" & tbl(i, cstフォルダ名) & "/" & Format(tbl(i, cst番号), "00") & "." & _
                               tbl(i, cst文書コード) & "." & tbl(i, cst文書名称) & "." & Format(tbl(i, cst承認日), "yyyy-mm-dd") & _
                               tbl(i, cst備考) & ".pdf" & """>" & tbl(i, cst文書名称) & "</a></td>"
                   
                    ElseIf j = 4 Then
                    
                        '何もしない

                    Else
                    
                        html = html & "<td>" & tbl(i, j) & "</td>"
                    End If
                    
                    
                Next j
                html = html & "</tr>" & vbNewLine
            
            Next i
            html = html & "</tbody>" & vbNewLine
            html = html & "</table>" & vbNewLine
            html = html & "<br>" & vbNewLine

            
        End If
        
    Next ws
    
    html = html & "</body>" & vbNewLine
    html = html & "</html>"
    
    'HTMLファイルの書き出し
    Stream.Open
    Stream.WriteText html
    Stream.SaveToFile FolderPath & FileName, 2 '上書き

    Stream.Close

End Sub

Sub GetFilesInfo()

    Dim MyFolder As String
    Dim MyFile As String
    Dim MyExtension As String
    Dim MySplit As Variant
    Dim MyRow As Long
    Dim MySheet As Worksheet
    Dim SelectedFolder As Variant
    
    ' フォルダを選択
    With Application.FileDialog(msoFileDialogFolderPicker)
        .Title = "フォルダを選択してください"
        .AllowMultiSelect = False
        If .Show <> -1 Then Exit Sub
        SelectedFolder = .SelectedItems(1)
    End With
    MyFolder = SelectedFolder & "\"
    
    Dim YenSymbolPosition As Integer
    YenSymbolPosition = InStrRev(CStr(SelectedFolder), "\")
    
    Dim FolderName As String
    FolderName = Mid(CStr(SelectedFolder), YenSymbolPosition + 1, Len(CStr(SelectedFolder)) - YenSymbolPosition)
    
    ' 新しいシートを作成
    Dim ws As Worksheet
    For Each ws In ThisWorkbook.Sheets
        If ws.Name = FolderName Then
            Application.DisplayAlerts = False ' 削除の警告を無効にする
            ws.Delete ' シートを削除する
            Application.DisplayAlerts = True ' 警告を再び有効にする
            Exit For ' for loop を抜ける
        End If
    Next ws
    
    Set MySheet = ThisWorkbook.Sheets.Add
    
   
    MySheet.Name = FolderName
    
    ' シートの1行目にヘッダーを設定
    MySheet.Cells(1, 1) = "No."
    MySheet.Cells(1, 2) = "文書番号"
    MySheet.Cells(1, 3) = "フォルダ名"
    MySheet.Cells(1, 4) = "文書名称"
    MySheet.Cells(1, 5) = "承認日（施行日）"
    MySheet.Cells(1, 6) = "備考"
    
    ' フォルダ内のファイルを取得
    MyFile = Dir(MyFolder & "*.*")
    
    ' ファイルを1つずつ処理
    Do While MyFile <> ""
        
        ' 拡張子が ".pdf" の場合
        MyExtension = Right(MyFile, 4)
        If MyExtension = ".pdf" Then
            
            ' ピリオドでファイル名を分割
            MySplit = Split(MyFile, ".")
            
            ' 表に情報を出力
            MyRow = MySheet.Cells(Rows.Count, 1).End(xlUp).Row + 1
            MySheet.Cells(MyRow, 1) = Format(CStr(MySplit(0)), "00")
            MySheet.Cells(MyRow, 2) = MySplit(1)
            MySheet.Cells(MyRow, 3) = FolderName
            MySheet.Cells(MyRow, 4) = MySplit(2)
            MySheet.Cells(MyRow, 5) = MySplit(3)
         
           
        End If
        
        ' 次のファイルを取得
        MyFile = Dir
        
    Loop
    
End Sub
