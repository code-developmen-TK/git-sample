Option Compare Database
Option Explicit

Function text_file_selection_dialog() As Variant
'************************************************************************
'参照設定を使用せずにアクセスでファイル選択ダイアログを使うには
'https://waq3-travelog.com/file-picker-dialog/
'
'
'**************************************************************************
Dim taget_file_name As Variant

On Error GoTo ErrHNDL  'エラー処理を宣言します。エラーが生じたら ErrHNDL 部分へ飛びます。

'ファイル参照用の設定値をセットします。
'ファイルを選択する場合は、msofiledialogfilepicker →　3（定数）
With Application.FileDialog(3)

    'ダイアログタイトル名
    .Title = "ファイルを選択してください"

     'ファイルの種類を定義します。
    .Filters.Clear
    .Filters.Add "テキストファイル", "*.txt,*.csv"

     '複数ファイル選択を可能にする場合はTrue、不可の場合はFalse。
     .AllowMultiSelect = False

     '最初に開くフォルダーを、当ファイルが存在しているフォルダーとします。
     .InitialFileName = CurrentProject.Path & "\"

     If .Show = -1 Then 'ファイルが選択されれば　-1 を返します。
         For Each taget_file_name In .SelectedItems
              text_file_selection_dialog = taget_file_name
         Next
     End If
     
End With

Exit Function

ErrHNDL:

     MsgBox Err.Number & vbCrLf & Err.Description
     Exit Function
     
End Function